<HTML>
<HEAD>
<TITLE>Startup Manager Utility</TITLE>
<SCRIPT LANGUAGE="VBScript">
Dim Sys, FSO, SH, StartUpFol, WinFol, ACount, SysFol, r, A1, INIOb, sINI, KRun, KRunServ, Opt, CR2, sID

'--*******UNCOMMENTED VERSION - see StartManCom.HTML For COMMENTS***----

 CR2 = vbcrlf & vbcrlf
 
 Sub window_onload()
      Box1.style.fontfamily = "arial"
      Box1.style.fontsize = 10
      TProps.style.fontfamily = "arial"
      TProps.style.fontsize = 12
      ACount = array(0, 0, 0, 0, 0, 0)  
 End Sub
 
Sub ButList_onclick()
   Box1.Options.Length = 0
    Set FSO = CreateObject("Scripting.FileSystemObject")
    Set SH = CreateObject("WScript.Shell")  
    Set Sys = CreateObject("JSSys3.Ops")   
    Set INIOb = CreateObject("JSSys3.INIEdit") 
     KRun = "Software\Microsoft\Windows\CurrentVersion\Run"
     KRunServ = "Software\Microsoft\Windows\CurrentVersion\RunServices"
     StartUpFol = SH.Specialfolders("StartUp")
     WinFol = FSO.GetSpecialFolder(0) 
     SysFol = FSO.GetSpecialFolder(1) 

        r = Sys.RegListStringData("HKLM", KRun, A1)
          If (r = 0) Then
             AddToList A1, "LMR", 0
          End If
          
        r = Sys.RegListStringData("HKLM", KRunServ, A1)
          If (r = 0) Then
             AddToList A1, "LMS", 1
          End If
          
        r = Sys.RegListStringData("HKCU", KRun, A1)
           If (r = 0) Then
             AddToList A1, "CUR", 2
          End If
           
        r = Sys.RegListStringData("HKCU", KRunServ, A1)
          If (r = 0) Then
             AddToList A1, "CUS", 3
          End If
 
   Dim oFol, oFil, LnkName, LnkOb, LnkPath, ALnks(), iA
     On Error Resume Next
        iA = -1    
    Set oFol = FSO.GetFolder(StartUpFol)
       For Each oFil in oFol.Files
         LnkName = oFil.Name
           If UCase(Right(LnkName, 3)) = "LNK" Then
              LnkName = oFil.Path
                Set LnkOb = SH.CreateShortcut(LnkName)
                   LnkPath = LnkOb.TargetPath
                Set LnkOb = Nothing   
                     If FSO.FileExists(LnkPath) = True Then   
                         LnkName =  LnkName & "^" & LnkPath  
                          iA = iA + 1  
                         ReDim preserve ALnks(iA)
                         ALnks(iA) = LnkName
                     End If       
           End If
       Next
   Set oFol = Nothing
   
    If (iA > -1) Then
        AddToList ALnks, "LNK", 4
    End If

   Dim s, s1, i7
     If FSO.FileExists(WinFol & "\WIN.INI") = True Then
         s = INIOb.GetIniVal(WinFol & "\WIN.INI", "windows", "load")
         s1 = INIOb.GetIniVal(WinFol & "\WIN.INI", "windows", "run")
            If (len(s) > 0) Then 
                Set Opt = document.createElement("OPTION")
                   Opt.text = s 
                   Opt.value = "load"  
                   Opt.ID = "WIN0"   
                   box1.Add Opt
                Set Opt = Nothing  
                  ACount(5) = 1  
            End If
             
            If (len(s1) > 0) Then 
                Set Opt = document.createElement("OPTION")
                    Opt.text = s1
                    Opt.value = "run"  
                    Opt.ID = "WIN1"
                    box1.Add Opt
                Set Opt = Nothing   
              i7 = ACount(5)
              i7 = i7 + 1
              ACount(5) = i7  
            End If
      End If  
   
    Set INIOb = Nothing
    Set Sys = Nothing
    Set SH = Nothing
    Set FSO = Nothing
End Sub

Sub AddToList(A6, sCat, iCount)
  Dim i6, s6, Pt6, sK6, sV6, iAc, PtE
   On Error Resume Next
     
   For i6 = 0 to UBound(A6)
        s6 = A6(i6)      
        Pt6 = InStr(1, s6, "^", 1)   
        If (Pt6 <> 0) and (Pt6 <> Len(s6)) and (InStr(1, s6, "RunDLL32.exe", 1) = 0) Then
            sV6 = Right(s6, (len(s6) - Pt6))  
                PtE = InStrRev(sV6, ".exe", -1, 1)
                   If (PtE <> 0) Then sV6 = Left(sV6, (PtE + 3))
                sK6 = Left(s6, (Pt6 - 1))        
                iAc = ACount(iCount)
                sID = sCat & CStr(iAc)   
                iAc = iAc + 1   
               ACount(iCount) = iAc
                  Set Opt = document.createElement("OPTION")  
                      Opt.text = sV6 
                      Opt.value = sK6 
                      Opt.ID = sID   
                     box1.Add Opt
                  Set Opt = Nothing   
        End If
      Next       
End Sub

Sub ButRem_onclick()
  Dim sVal, sEXE, iSel, L3, sDelVal
         If (Box1.value = "0") Then Exit Sub  
       On Error Resume Next  
      
    iSel = Box1.selectedindex
    sEXE = Box1.Options(iSel).text
    sVal = Box1.Options(iSel).value
    sID =  Box1.Options(iSel).id
  r = MsgBox("Remove " & sEXE & " from startup?", 36, "Confirm")  
    If (r = 7) Then Exit Sub  
  
  L3 = Left(sID, 3)
   Select Case L3        
        Case "LMR", "LMS", "CUR", "CUS"
               If L3 = "LMR" Then
                    sDelVal = "HKLM\" & KRun & "\" & sVal
               ElseIf L3 = "LMS" Then
                    sDelVal = "HKLM\" & KRunServ & "\" & sVal  
               ElseIf L3 = "CUR" Then
                    sDelVal = "HKCU\" & KRun & "\" & sVal 
               Else
                    sDelVal = "HKCU\" & KRunServ & "\" & sVal   
               End If    
                 Set SH = CreateObject("WScript.Shell")  
                     SH.RegDelete sDelVal
                 Set SH = Nothing  
        
        Case "LNK"
               Set FSO = CreateObject("Scripting.FileSystemObject")
                 If FSO.FileExists(sVal) = True Then
                   FSO.DeleteFile sVal, True
                 End If  
               Set FSO = Nothing
       
        Case "WIN"
                Set INIOb = CreateObject("JSSys3.INIEdit") 
                   r = INIOb.WriteIniVal(WinFol & "\WIN.INI", "windows", sVal, "")
                Set INIOb = Nothing
        Case Else
              MsgBox "Error. Unable to find information for removing this item.", 64
              Exit Sub
   End Select   
    ButList.Value = "Refresh List" 
End Sub

Sub ButProp_onclick()
Dim sPath, FilOb, s3, iSel
          If (Box1.value = "0") Then Exit Sub
    TProps.innertext = "Processing..."
    iSel = Box1.selectedindex
    sPath = Box1.Options(iSel).text
   Set FSO = CreateObject("Scripting.FileSystemObject")
      If FSO.FileExists(sPath) = False Then
          If FSO.FileExists(WinFol & "\" & sPath) Then
              sPath = WinFol & "\" & sPath
          Else
              sPath = SysFol & "\" & sPath
          End If
     End If
     
        If FSO.FileExists(sPath) = False Then
            TProps.innertext = "Invalid file path."
             Set FSO = Nothing
            Exit Sub
        End If    
        
   Set FSO = Nothing         
   Set Sys = CreateObject("JSSys3.Ops")
   On Error Resume Next
     Set FilOb = Sys.GetPEFile(sPath)
         If Err.number = 0 Then
                s3 = "Name: " & FilOb.Name & CR2
                s3 = s3 & "CompanyName: " & FilOb.CompanyName & CR2
                s3 = s3 & "FileVersion: " & FilOb.FileVersion & CR2
                s3 = s3 & "DateCreated: " & FilOb.DateCreated & CR2
                s3 = s3 & "DateLastModified: " & FilOb.DateLastModified & CR2
                s3 = s3 & "ProductVersion: " & FilOb.ProductVersion & CR2
                s3 = s3 & "ProductName: " & FilOb.ProductName & CR2
                s3 = s3 & "FileDescription: " & FilOb.FileDescription & CR2
                s3 = s3 & "Size: " & FilOb.Size &  " KB" & vbcrlf
                 TProps.innertext = s3
         Else
                TProps.innertext = Err.Description
         End If
     Set FilOb = Nothing
 Set Sys = Nothing          
End Sub

</SCRIPT>

<!------------------- START HTML ------------------------------------------------->
</HEAD>
<BODY BGCOLOR="#03AEE7" TEXT="#000060" MARGINWIDTH=40 MARGINHEIGHT=20 LEFTMARGIN=40 TOPMARGIN=20>
<DIV align="center">
<BR>
<FONT FACE="arial" COLOR="#0000A0" SIZE=2>
<H2>Startup Manager Utility</H2>
</FONT>

<!------------------------- Explanation table ------------------------------------- -->
<TABLE BORDER=2 BORDERCOLOR="#0000A0" WIDTH=740 CELLPADDING=10><TR><TD align="left" BGCOLOR="#B4ECFE">
<FONT FACE="arial" COLOR="#0000A0" SIZE=2>
&#160; &#160; This webpage is a Windows StartUp Manager. It lists all programs
set to run at startup, getting the list from the Registry, StartUp folder and WIN.INI.
The component JSSys3.dll must be installed for this webpage to work. The version
of JSSys3.dll must be at least 3.03.0210 (required to get file properties for startup programs).<BR><BR>

&#160; &#160; When this utility is used Internet Explorer will display a message that the page may not be safe.
That is because the script in the page uses several objects that are not "safe for scripting":
The FileSystemObject, WScript.Shell and JSSys3.dll. See the included file for information if you
want to adjust Internet Explorer security.<BR><BR>

&#160; &#160; It's possible that later versions of IE6 may not run this page at all. If that is
the case, and you haven't disabled HTA, you could try renaming the file to StartMan.hta. Also,
deleting an entry does not work in IE4. IE4 raises an error saying that the OPTION ID cannot
be changed at runtime. As a result the IDs hold no data and the utility will report "Unable to
find information needed to delete selected entry."<BR><BR>
</FONT>
</TD></TR></TABLE>



<! ---------------- warning table ------------------------------------------------------->

<TABLE BGCOLOR="#FFE8D2" BORDER=2 BORDERCOLOR="#0000A0" WIDTH=740 CELLPADDING=10><TR><TD align="center">
<FONT FACE="arial" COLOR="#813E01" SIZE=3>
<B>CAUTION: </B><BR>
</FONT>
<FONT FACE="arial" COLOR="#813E01" SIZE=2> <B>
You use this utility at your own risk.<BR>
Do not remove programs from startup if you are not certain that it is safe.<BR>
Some startup programs are necessary.</B><BR><BR>

Programs that should be safe to remove include music players and any other programs
that have no reason to be constantly running. If they have been placed in the startup list
they're probably spyware. Also, most hardware installs startup programs and in most
cases it is not necessary. Frivolous printer utilities, CDR utilities, Zip drive programs, etc.
should generally be safe to remove if you do not use them.<BR><BR>
</FONT>
</TD></TR></TABLE>


<! ------------ end warning -------------------------------------------->

<! ------------ usage info. table  --------------------------------------->

<TABLE BORDER=2 BORDERCOLOR="#0000A0" WIDTH=740 CELLPADDING=10><TR><TD align="center" BGCOLOR="#B4ECFE">
<FONT FACE="arial" COLOR="#0000A0" SIZE=2>
<H4>To use this utility:</H4>
&#149; Click Get List to show list of startup programs.<BR>
&#149; Select an item and click Properties to get information about the program.<BR>
&#149; Click Remove to remove selected item from startup.<BR><BR>
</FONT>
</TD></TR></TABLE>
 
<!----------------- ops table - row of headers  ----------------------------------->

<TABLE BORDER=4 BORDERCOLOR="#D57171" WIDTH=740 CELLPADDING=10>  
<TR>
<TD align="center" WIDTH=300 BGCOLOR="#6FDAFD">
 <FONT FACE="arial" SIZE=2 COLOR="#0000A0"><B>
               Startup Programs</B>
</FONT>
</TD>
<TD align="center" WIDTH=140 BGCOLOR="#6FDAFD">
<FONT FACE="arial" SIZE=2 COLOR="#0000A0"><B>
               Options</B>
</FONT>
</TD>
<TD align="center" WIDTH=300 BGCOLOR="#6FDAFD">
<FONT FACE="arial" SIZE=2 COLOR="#0000A0"><B>
         Properties of Selected Item:</B>    
</FONT>
</TD>
</TR>

<!------------------ List Box TD ----------------------------------------------- -->
    <!-- this is the SELECT box for startup list. It gets ID = Box1 for use in script.  -->
<TR> <TD align="top" WIDTH=300 BGCOLOR="#CFEFFE">
               <SELECT ID="Box1" SIZE=18>
               </SELECT>
       </TD>

<!---------------- Buttons TD ------------------------------------------------- -->

<TD align="center" WIDTH=140 BGCOLOR="#CFEFFE">
<FONT FACE="arial" SIZE=2 COLOR="#0000A0">
  Get list of <BR>startup programs:<BR><BR>

<!---------Get List button: "ButList"   -------------------->
 <INPUT NAME="ButList" TYPE="button" VALUE="Get List"></INPUT><BR><BR>

<!---------Get Properties button: "ButProp"   -------------------->
   Get properties for <BR>selected item:<BR><BR>
  <INPUT NAME="ButProp" TYPE="button" VALUE="Properties"></INPUT><BR><BR>

<!---------Remove button: "ButRem"   -------------------->
  Remove selected item<BR> from startup:<BR><BR>
<INPUT NAME="ButRem" TYPE="button" VALUE="Remove"></INPUT><BR><BR>
</FONT>
</TD>

<!----------------- File Properties TD ----------------------------------------- -->
  <!-- this is the section on right side that displays file info. It gets ID TProps  ----->

<TD ALIGN="top" WIDTH=300 ID="TProps" BGCOLOR="#CFEFFE">
<FONT FACE="arial" SIZE=2 COLOR="#0000A0">
</FONT>
</TD>
       
</TR></TABLE>
<BR><HR><BR>
<FONT FACE="arial" COLOR="#0000A0" SIZE=2>
<B>JSWare &#149; www.jsware.net</B><BR><BR>

</FONT>
</DIV>
</BODY></HTML>

